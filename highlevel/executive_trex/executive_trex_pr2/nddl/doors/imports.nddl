#include "nav/imports.nddl"
#include "world_model/topological_map.nddl"
#include "rcs/doors/exports.nddl"

/** External Functions to be integrated **/

/**
 * @brief Obtain the base pose at a given distance normal to the door frame. The token object has all the parameters
 * of a door message.
 */
constraint  door_get_robot_pose_for_door(x, y, z, qx, qy, qz, qw, token_object, distance){
  x <: numeric && y <: x && z <: x && 
  qx <: x && qy <: x && qz <: x && qw <: x &&
  token_object <: Object && distance <: x
}

// Additional mechanisms used
RightGripperMechanism right_gripper_mechanism = new RightGripperMechanism(External);

// Additional actions for the doro domain
DoorsTuckArms doors_tuck_arms = new DoorsTuckArms(External);
ReleaseDoor release_door = new ReleaseDoor(External);
OpenDoor open_door = new OpenDoor(External);
GraspHandle grasp_handle = new GraspHandle(External);
DetectHandle detect_handle = new DetectHandle(External);
DetectDoor detect_door = new DetectDoor(External);
OpenDoorWithoutGrasp open_door_without_grasp = new OpenDoorWithoutGrasp(External);
CheckPath check_path = new CheckPath(External);
NotifyDoorBlocked notify_door_blocked = new NotifyDoorBlocked(External);

// Controller requirements
DoorsTuckArms::Active{
  contained_by(right_arm_mechanism.use_joint_trajectory_controller);
}

DetectDoor::Active{
  contained_by(laser_tilt_mechanism.use_laser_tilt_controller);
  contained_by(right_arm_mechanism.use_joint_trajectory_controller);
}

DetectHandle::Active{
  contained_by(laser_tilt_mechanism.use_laser_tilt_controller);
  contained_by(head_mechanism.use_head_controller);
  contained_by(right_arm_mechanism.use_joint_trajectory_controller);
}

GraspHandle::Active{
  contained_by(right_arm_mechanism.use_composite_cartesian_controller);
  contained_by(right_gripper_mechanism.use_effort_controller);
}

OpenDoor::Active{
  contained_by(right_arm_mechanism.use_composite_tff_controller);
  contained_by(right_gripper_mechanism.use_effort_controller);
}




