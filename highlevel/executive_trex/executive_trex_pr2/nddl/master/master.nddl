/*
 * Copyright (c) 2008, Willow Garage, Inc.
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the <ORGANIZATION> nor the names of its
 *       contributors may be used to endorse or promote products derived from
 *       this software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

#include "TREX.nddl"
#include "world_model/topological_map.nddl"
#include "rcs/exports.nddl"
#include "rcs/nav/exports.nddl"
#include "rcs/plugs/exports.nddl"
#include "nav/exports.nddl"
#include "plugs/exports.nddl"

// Priority for goals
typedef int [0 5] PRIORITY;

class M2Goals extends Behavior {
  predicate Active{
    string frame_id;
    float time_stamp;
    float x;
    float y;
    float z;
    float qx;
    float qy;
    float qz;
    float qw;
    bool recharge;
  }

  predicate Inactive{}

  M2Goals(){
    super(Internal);
  }
};

class M2Controller extends AgentTimeline {
  predicate Holds {
    bool active;
  }

  M2Controller(){
    super(Internal, "Holds");
  }
};

// Internal Timeines
M2Goals m2_goals = new M2Goals();
M2Controller m2_controller = new M2Controller();

// External timelines
MoveBehavior driver = new MoveBehavior(External, false);
MoveBehavior doorman = new MoveBehavior(External, true);
Recharger recharger = new Recharger(External);

// Actions
AgentActions actions = new AgentActions();

M2Goals::Active{
  // Linking with a start event to force execution
  contains(actions.startEvent);

  if(start){
    starts(m2_controller.Holds mc);
    eq(mc.active, true);
  }
}

/**
 * The loop for master level control. It uses the doorman, driver and recharger. 
 */
M2Controller::Holds{
  if(active == false){
    contained_by(m2_goals.Inactive);
  }

  // Default based on predecessors
  met_by(Holds p);
  defaultOnCommit(active, p.active);

  if(active == true){
    contained_by(m2_goals.Active m2_active);

    float current_x, current_y, current_z, current_qx, current_qy, current_qz, current_qw;
    tf_get_robot_pose(current_x, current_y, current_z, current_qx, current_qy, current_qz, current_qw, "map");

    // Are we at the goal?
    float distance;
    calcDistance(distance, current_x, current_y, m2_active.x, m2_active.y);
    bool at_goal;
    testLEQ(at_goal, distance, 1.5);

    if(at_goal == true){
      bool recharge_required;
      eq(recharge_required, m2_active.recharge);
      if(recharge_required == true){
	contains_start(recharger.Active cmd);
	concurrent(cmd.end, end);
	eq_pose_msg(cmd.object, m2_active.object);
      }
      else {
	meets(Holds s);
	eq(s.active, false);
	meets(m2_goals.Inactive m2_inactive);
	eq(m2_inactive.status, SUCCESS);
      }
    }
    else{
      // Take the next step towards the goal. This will either navigate to the goal, or navigate to the
      // next connector along the way. It would be ideal to be able to specify for the nav stack that
      // we do not care about orientation, and that we want to get as close as possible to the goal, but the criteria
      // for success is based on a given error bound. Thus we could get close if possible, but permit a successful termination
      // when we cannot get any closer but are within that bound. This is desirable since the precise approach is based
      // on cacluations that deal with position of approach and location of the socket or door and these are encoded internally
      // in the domains
      contains_start(MoveBehavior.Active cmd);
      concurrent(cmd.end, end);
      eq(cmd.frame_id, "map");

      // Determine the next map point to go for. It will either be the outlet
      // or it will be a doorway en-route to the outlet
      bool thru_doorway;
      map_get_next_move(cmd.x, cmd.y, cmd.y, cmd.qx, cmd.qy, cmd.qz, cmd.qw,
			thru_doorway, current_x, current_y, m2_active.x, m2_active.y);

      if(thru_doorway == true){
	eq(cmd.object, doorman);
      }
      else{
	eq(cmd.object, driver);
      }
    }
  }
}

MoveBehavior::Inactive{
  if(start){
    starts(m2_controller.Holds mc);
    if(mc.active == true){
      if(status == ABORTED){
	eq(mc.active, false);
	starts_during(m2_goals.Inactive m2_inactive);
	eq(m2_inactive.status, ABORTED);
      }
    }
  }
}

Recharger::Inactive{
  if(start){
    starts(m2_controller.Holds mc);
    if(mc.active == true){
      if(status == UNDEFINED){}
      else{
	starts_during(m2_goals.Inactive m2_inactive);
	eq(mc.active, false);
	eq(m2_inactive.status, status);
      }
    }
  }

}

